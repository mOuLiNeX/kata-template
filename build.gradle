plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.6.0'
    id "org.sonarqube" version "3.1.1"
    id "com.github.ben-manes.versions" version "0.38.0"
}

group 'kata'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml {
            enabled true
            destination file("${buildDir}/reports/jacoco/report.xml")
        }
        html.enabled true
        csv.enabled false
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.assertj:assertj-core:3.+'
}

pitest {
    junit5PluginVersion = '0.12'
    threads = 2
    outputFormats = ['HTML']
}

test {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.projectName", "kata-template"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/report.xml"
    }
}


apply plugin: 'com.github.ben-manes.versions'

// To disallow release candidates as upgradable versions
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}